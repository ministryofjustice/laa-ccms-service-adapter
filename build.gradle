import uk.co.boothen.gradle.wsimport.WsImport

plugins {
    id 'java'
    id 'org.springframework.boot' version '4.0.2'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'maven-publish'
    id 'uk.co.boothen.gradle.wsimport' version '0.25'
    id 'net.researchgate.release' version '3.1.0'
}

java {
    toolchain.languageVersion.set(JavaLanguageVersion.of(8))
}

repositories {
    mavenCentral()
}

configurations {
    configureEach {
        exclude group: "org.springframework.boot", module: "spring-boot-starter-logging"
    }
}

dependencies {
    implementation "org.springframework.boot:spring-boot-starter"
    implementation "org.springframework.boot:spring-boot-starter-log4j2"
    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation 'io.micrometer:micrometer-registry-prometheus'
    implementation "org.hibernate.validator:hibernate-validator:9.1.0.Final"

    implementation "org.apache.cxf:cxf-spring-boot-starter-jaxws:4.1.4"
    implementation "org.apache.cxf:cxf-rt-ws-security:4.1.4"
    implementation "org.apache.cxf.xjcplugins:cxf-xjc-ts:4.1.4"

    implementation 'org.apache.commons:commons-lang3:3.20.0'

    implementation 'org.mapstruct:mapstruct-jdk8:1.6.3'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.6.3'

    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-csv"

    implementation "javax.xml.bind:jaxb-api:2.3.1"
    implementation "com.sun.xml.bind:jaxb-core:4.0.6"
    implementation "com.sun.xml.bind:jaxb-impl:4.0.6"
    implementation "javax.activation:activation:1.1.1"

    compileOnly "org.springframework.boot:spring-boot-starter-tomcat"
    testImplementation "org.springframework.boot:spring-boot-starter-test"
}

configurations {
    wsdl2java
}

tasks.register("updateSnapshotVersion") {
    doLast {
        def gitHash = "git rev-parse --short HEAD".execute().text.trim()
        def propertiesFile = file('gradle.properties')
        def properties = new Properties()
        properties.load(new FileInputStream(propertiesFile))

        def currentVersion = properties.getProperty('version')
        def newVersion = currentVersion.replace('-SNAPSHOT', "-${gitHash}-SNAPSHOT")
        properties.setProperty('version', newVersion)
        properties.store(propertiesFile.newWriter(), null)
    }
}

publishing {
    repositories {
        maven {
            url "https://maven.pkg.github.com/ministryofjustice/${rootProject.name}"
            credentials {
                username = System.getenv("GITHUB_ACTOR")?.trim()
                password = System.getenv("GITHUB_TOKEN")?.trim()
            }
        }
    }
}

tasks.register('wsimport', WsImport) {
    target = '2.0'
    keep = true
    extension = true
    verbose = false
    quiet = true
    debug = false
    xadditionalHeaders = true

    wsdl('AssessService-12.2.1.wsdl') {
        wsdlLocation = '/wsdl/Answer.wsdl'
    }
    wsdl('opa10assess.wsdl') {
        wsdlLocation = '/wsdl/opa10assess.wsdl'
    }
}

// todo remove when moving to main
release {
    git {
        requireBranch.set('main-cp')
    }
}
