
plugins {
    id 'java'
    id 'org.springframework.boot' version '3.0.13'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'maven-publish'
    id 'uk.co.boothen.gradle.wsimport' version '0.23'
    id 'net.researchgate.release' version '3.1.0'
}

java {
    toolchain.languageVersion.set(JavaLanguageVersion.of(17))
}

repositories {
    maven { url "https://build.shibboleth.net/nexus/content/repositories/releases/" }
    mavenCentral()
}

configurations {
    configureEach {
        exclude group: "org.springframework.boot", module: "spring-boot-starter-logging"
    }
}

dependencies {
    implementation "org.springframework.boot:spring-boot-starter"
    implementation "org.springframework.boot:spring-boot-starter-log4j2"
//    jaxWsTools "com.sun.xml.ws:jaxws-rt:4.0.3"
//    runtimeOnly "org.glassfish.jaxb:jaxb-runtime:3.0.2"
    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation 'io.micrometer:micrometer-registry-prometheus'
    implementation "org.hibernate.validator:hibernate-validator:6.2.5.Final"

    implementation "org.apache.cxf:cxf-spring-boot-starter-jaxws:4.0.8"
    implementation "org.apache.cxf:cxf-rt-ws-security:4.0.8"
//    implementation "org.apache.cxf.xjcplugins:cxf-xjc-ts:3.2.1"

    implementation 'org.apache.commons:commons-lang3:3.17.0'

    implementation 'org.mapstruct:mapstruct-jdk8:1.2.0.Final'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.2.0.Final'

    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-csv"

//    implementation "com.sun.xml.ws:jaxws-rt:4.0.3"
//    implementation "jakarta.xml.bind:jakarta.xml.bind-api:4.0.2"
//    implementation "jakarta.xml.ws:jakarta.xml.ws-api:4.0.2"
    implementation "javax.activation:activation:1.1.1"

    compileOnly "org.springframework.boot:spring-boot-starter-tomcat"
    testImplementation "org.springframework.boot:spring-boot-starter-test"
}

configurations {
    wsdl2java
}

tasks.register("updateSnapshotVersion") {
    doLast {
        def gitHash = "git rev-parse --short HEAD".execute().text.trim()
        def propertiesFile = file('gradle.properties')
        def properties = new Properties()
        properties.load(new FileInputStream(propertiesFile))

        def currentVersion = properties.getProperty('version')
        def newVersion = currentVersion.replace('-SNAPSHOT', "-${gitHash}-SNAPSHOT")
        properties.setProperty('version', newVersion)
        properties.store(propertiesFile.newWriter(), null)
    }
}

publishing {
    repositories {
        maven {
            url "https://maven.pkg.github.com/ministryofjustice/${rootProject.name}"
            credentials {
                username = System.getenv("GITHUB_ACTOR")?.trim()
                password = System.getenv("GITHUB_TOKEN")?.trim()
            }
        }
    }
}

wsimport {
    target = '3.0'
    keep = true
    extension = true
    verbose = false
    quiet = true
    debug = false
    xadditionalHeaders = true

    wsdl('AssessService-12.2.1.wsdl') {
        wsdlLocation = '/wsdl/Answer.wsdl'
    }
    wsdl('opa10assess.wsdl') {
        wsdlLocation = '/wsdl/opa10assess.wsdl'
    }
}

// todo remove when moving to main
release {
    git {
        requireBranch.set('main-cp')
    }
}
tasks.withType(Test).configureEach {
    useJUnitPlatform()
}
