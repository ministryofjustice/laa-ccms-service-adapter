plugins {
    id 'java'
    id 'maven-publish'
    id 'org.springframework.boot' version '3.5.0'
    id 'uk.co.boothen.gradle.wsimport' version '0.22'
    id 'io.spring.dependency-management' version '1.1.7'
}

group = 'uk.gov.justice.laa.ccms'
description = 'OPA10 to OPA 12 Assess Service Adapter'

java {
    toolchain.languageVersion.set(JavaLanguageVersion.of(21))
}

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.springframework.boot:spring-boot-starter"
    implementation "org.springframework.boot:spring-boot-starter-log4j2"
    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation "org.hibernate.validator:hibernate-validator:9.0.0.Final"
    implementation "org.apache.cxf:cxf-spring-boot-starter-jaxws:4.1.2"
    implementation "org.apache.cxf:cxf-rt-ws-security:3.6.7"
    implementation 'org.apache.commons:commons-lang3:3.17.0'
    implementation "org.mapstruct:mapstruct:1.6.3"
    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-csv:2.19.0"
    implementation 'jakarta.xml.bind:jakarta.xml.bind-api:4.0.2'
    implementation 'jakarta.xml.ws:jakarta.xml.ws-api:4.0.2'
    implementation 'jakarta.activation:jakarta.activation-api:2.1.3'

    compileOnly "org.springframework.boot:spring-boot-starter-tomcat"
    testImplementation "org.springframework.boot:spring-boot-starter-test"
}

configurations {
    wsdl2java
}

tasks.register("updateSnapshotVersion") {
    doLast(task -> {
        def gitHash = "git rev-parse --short HEAD".execute().text.trim()
        def propertiesFile = file('gradle.properties')
        def properties = new Properties()
        properties.load(new FileInputStream(propertiesFile))

        def currentVersion = properties.getProperty('version')
        def newVersion = currentVersion.replace('-SNAPSHOT', "-${gitHash}-SNAPSHOT")
        properties.setProperty('version', newVersion)
        properties.store(propertiesFile.newWriter(), null)
    }
    )
}

publishing {
    repositories {
        maven {
            url "https://maven.pkg.github.com/ministryofjustice/${rootProject.name}"
            credentials {
                username = System.getenv("GITHUB_ACTOR")?.trim()
                password = System.getenv("GITHUB_TOKEN")?.trim()
            }
        }
    }
}

wsimport {
    generatedSourceRoot = "/generated/src/wsdl/main"
    generatedClassesRoot = "/classes/main"
    target = "3.0"
    keep = true
    extension = true
    verbose = false
    quiet = true
    debug = false
    xnocompile = true
    xadditionalHeaders = true

    wsdl('AssessService-12.2.1.wsdl') {
        wsdlLocation = '/wsdl/Answer.wsdl'
    }
    wsdl('opa10assess.wsdl') {
        wsdlLocation = '/wsdl/opa10assess.wsdl'
    }
}
